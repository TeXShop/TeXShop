#!/bin/tcsh

if ("$#argv" == "0") then
	echo "$0 Missing argument: eps-file-name"
	exit 1
endif

set filename = "$argv[1]"
set outputname = "${filename:r}.pdf"
set tempname = "temp"
set tempoutputname = "temp.pdf"
set errorname = "error"

# Create tmp directory from process number
onintr interrupt
set tmpdir = "/tmp/epstopdf1.$$"

if (-d ${tmpdir}) then
	echo "epstopdf1: ${tmpdir} already exists. Bailing out."
	exit 1
else
	mkdir ${tmpdir}
endif

set fulltempname = "${tmpdir}/${tempname}"
set fulltempoutputname = "${tmpdir}/${tempoutputname}"
set newtempname = "${tmpdir}/${filename}"
set fullerrorname = "${tmpdir}/${errorname}"

epstopdf --nogs -o="${fulltempname}" "${filename}" >& "${fullerrorname}"
set epstopdf = `cat "${fullerrorname}"`
set requiredmessage = "==> Warning: BoundingBox not found!"

if ("${epstopdf}" == "${requiredmessage}") then
	tr "\015" "\012" <"${filename}" >"${newtempname}"
	epstopdf --nogs -o="${fulltempname}" "${newtempname}" >& "${fullerrorname}"
	set epstopdf = `cat "${fullerrorname}"`
	if ("${epstopdf}" == "${requiredmessage}") then
		echo "epstopdf1: fix linefeeds + epstopdf failed. Bailing out."
		exit 1
	endif
endif

/usr/local/bin/gs -dCompatibilityLevel=1.2 -q -dNOPAUSE -dBATCH -sDEVICE=pdfwrite -sOutputFile=${fulltempoutputname}  -c save pop -f ${fulltempname}
mv "${fulltempoutputname}" "${outputname}"

interrupt:
# remove tmp dir
rm -rf ${tmpdir}
